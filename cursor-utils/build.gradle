import com.android.builder.core.BuilderConstants;

apply plugin: 'com.android.library'
apply plugin: 'robolectric'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 2
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (!name.equals(BuilderConstants.DEBUG)) {
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }
}

dependencies {
    provided 'com.android.support:support-v4:21.0.3'
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile('org.robolectric:robolectric:2.4') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
}

// Reenable after https://github.com/chrisbanes/gradle-mvn-push/pull/45 is submitted
// apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
apply from: 'https://raw.githubusercontent.com/ronshapiro/gradle-mvn-push/c14f8ab289a696cdc58c421f1dc28894e3ba4989/gradle-mvn-push.gradle'